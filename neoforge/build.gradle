plugins { id "com.github.johnrengelman.shadow" version "7.1.2" }
architectury { neoForge() }

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
}

repositories {
    maven { url "https://maven.neoforged.net/releases" }
    exclusiveContent {
        forRepository { maven { url "https://cursemaven.com/" } }
        filter { includeGroup "curse.maven" }
    }
    exclusiveContent {
        forRepository { maven { url "https://api.modrinth.com/maven" } }
        filter { includeGroup "maven.modrinth" }
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${parent.props.neoforge}"
    modImplementation "maven.modrinth:serene-seasons:${parent.props.sereneseasons_neoforge}"
    modImplementation "maven.modrinth:glitchcore:${parent.props.glitchcore_neoforge}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }
}

processResources {
    def props = [
            "id"              : rootProject.id,
            "name"            : rootProject.displayname,
            "description"     : rootProject.description,
            "author"          : rootProject.author,
            "license"         : rootProject.license,
            "websiteUrl"      : rootProject.website,
            "issueTrackerUrl" : rootProject.issues,
            "forgeUpdateUrl"  : rootProject.updater,
            "version"         : project.version,
            "minecraftVersion": rootProject.minecraft,
            "fabricVersion"   : rootProject.props.fabric,
            "forgeVersion"    : rootProject.props.forge,
            "neoForgeVersion" : rootProject.props.neoforge,
    ]

    inputs.properties props
    filesMatching("META-INF/neoforge.mods.toml") { expand props }
    filesMatching("META-INF/mods.toml") { expand props } // For NeoForge 1.20.4
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set "dev-shadow"
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set null
}

jar {
    archiveClassifier.set "dev"
    dependsOn processResources
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}
